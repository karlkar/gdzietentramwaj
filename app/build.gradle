apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.kksionek.gdzietentramwaj"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 56
        versionName "1.56"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [enableCrashReporting: "false"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            manifestPlaceholders = [enableCrashReporting: "true"]
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    flavorDimensions "tier"
    productFlavors {
        paid {
            dimension "tier"
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
        }
        free {
            dimension "tier"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    def nav_version = "2.3.0"
    def daggerVersion = '2.28.3'
    def roomVersion = "2.2.5"
    def lifecycleVersion = "2.2.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "com.google.android.material:material:1.2.1"

    implementation "androidx.constraintlayout:constraintlayout:2.0.1"

    implementation "androidx.preference:preference-ktx:1.1.1"

    implementation "com.google.android.gms:play-services-maps:17.0.0"
    implementation "com.google.android.gms:play-services-location:17.1.0"

    implementation "com.google.maps.android:android-maps-utils:0.5"

    implementation "com.squareup.retrofit2:retrofit:2.7.1"
    implementation "com.squareup.retrofit2:converter-gson:2.7.1"
    implementation "com.squareup.retrofit2:converter-scalars:2.7.1"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"

    implementation "com.squareup.picasso:picasso:2.71828"

    implementation "com.google.code.gson:gson:2.8.5"

    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:2.28.3"
    implementation "com.google.dagger:dagger-android-support:2.28.3"

    implementation "androidx.multidex:multidex:2.0.1"

    implementation "com.squareup.okhttp3:logging-interceptor:4.4.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    freeImplementation "com.google.firebase:firebase-analytics:17.5.0"
    freeImplementation "com.google.firebase:firebase-ads:19.4.0"
    implementation 'com.google.firebase:firebase-crashlytics-ktx:17.2.1'

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.fragment:fragment-ktx:1.2.5"

    implementation "com.google.android.play:core:1.8.2"

    implementation("org.simpleframework:simple-xml:2.7.1") {
        exclude module: "stax"
        exclude module: "stax-api"
        exclude module: "xpp3"
    }

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation "junit:junit:4.12"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.amshove.kluent:kluent:1.61"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
}
