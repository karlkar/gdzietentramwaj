apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.kksionek.gdzietentramwaj"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 51
        versionName "1.51"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [enableCrashReporting:"false"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            manifestPlaceholders = [enableCrashReporting:"true"]
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        buildTypes.each {
            it.buildConfigField 'String', 'ZTM_API_KEY', project.findProperty("ztmApiKey") ?: System.getenv("ZTM_API_KEY")
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "tier", "store"
    productFlavors {
        paid {
            dimension "tier"
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
        }
        free {
            dimension "tier"
        }
        google {
            dimension "store"
            buildConfigField "boolean", "FEATURE_PLAY_STORE", "true"
        }
        huawei {
            dimension "store"
            buildConfigField "boolean", "FEATURE_PLAY_STORE", "false"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("paid") && names.contains("huawei")) {
            setIgnore(true)
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    def nav_version = "2.2.0-rc02"
    def daggerVersion = '2.22'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    implementation 'com.google.maps.android:android-maps-utils:0.5'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.6.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.15'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'androidx.lifecycle:lifecycle-runtime:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    kapt 'androidx.lifecycle:lifecycle-common-java8:2.1.0'
    implementation 'androidx.room:room-runtime:2.2.2'
    implementation 'androidx.room:room-rxjava2:2.2.2'
    kapt 'androidx.room:room-compiler:2.2.2'

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:2.24"
    implementation "com.google.dagger:dagger-android-support:2.24"

    implementation 'androidx.multidex:multidex:2.0.1'

    // DEBUG:
    //        implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    freeImplementation 'com.google.firebase:firebase-core:17.2.1'
    freeImplementation 'com.google.firebase:firebase-ads:18.3.0'
    freeImplementation 'androidx.browser:browser:1.0.0'

    implementation 'com.google.firebase:firebase-config:19.0.3'

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.google.android.play:core:1.6.4'

    implementation("org.simpleframework:simple-xml:2.7.1") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    androidTestImplementation 'androidx.room:room-testing:2.2.2'
    androidTestImplementation 'androidx.test:runner:1.2.0'
}
